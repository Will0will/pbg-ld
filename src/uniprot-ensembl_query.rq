--
-- SPARQL query to output table of (alternative) gene names and descriptions including
-- chromosomal information using UniProt tomato reference proteome and EnsEMBL Plants.
-- 

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX uni: <http://purl.uniprot.org/core/>

SELECT
   ?chr
   ?gene_name
   ?alt_gene_name
   sql:GROUP_CONCAT_DISTINCT(?des, '; ') AS ?des
WHERE {
   GRAPH <http://www.uniprot.org/proteomes/Solanum_lycopersicum> {
      ?prot a uni:Protein .
      ?prot uni:encodedBy ?gene .
      ?prot uni:annotation/rdfs:comment ?des .
      ?prot rdfs:seeAlso ?xref .
   }
   GRAPH <http://plants.ensembl.org/Solanum_lycopersicum> {
      ?xref faldo:location/rdfs:label ?loc .
      BIND(replace(replace(?loc, '.+\\s', ''), ':.+', '') AS ?chr) .
   }
   OPTIONAL { ?gene skos:prefLabel ?gene_name }
   OPTIONAL { ?gene skos:altLabel ?alt_gene_name }
   FILTER(?gene_name || ?alt_gene_name) .
}
GROUP BY ?chr ?gene_name ?alt_gene_name
