-- 
-- SPARQL queries to fix cross-references in EnsEMBL RDF graph.
--
-- Prerequisite: GRANT SPARQL_UPDATE to "SPARQL";
--

-- GO and KEGG
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

WITH <http://plants.ensembl.org/Solanum_lycopersicum>
DELETE { ?s rdfs:seeAlso ?o }
INSERT { ?s rdfs:seeAlso ?fixed }
WHERE
{ 
   ?s rdfs:seeAlso ?o .
   FILTER regex(?o, 'http://identifiers.org/(go|kegg)') .
   BIND(uri(replace(str(?o), '%253A', ':')) AS ?fixed)
}

-- SIO 'is_paralogous_to'
WITH <http://plants.ensembl.org/Solanum_lycopersicum>
DELETE { ?s <http://semanticscience.org/resource/SIO:000630> ?o }
INSERT { ?s <http://semanticscience.org/resource/SIO_000630> ?o }
WHERE { ?s <http://semanticscience.org/resource/SIO:000630> ?o }

-- EnsEMBL transcripts
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

WITH <http://plants.ensembl.org/Solanum_lycopersicum>
DELETE { ?s rdfs:seeAlso ?o }
INSERT { ?s rdfs:seeAlso ?fixed }
WHERE {
   ?s a obo:SO_0000120 .
   ?s rdfs:seeAlso ?o .
   FILTER regex(?o, 'http://identifiers.org/ensembl') .
   BIND(uri(replace(str(?o), 'ensembl', 'ensembl.plant')) AS ?fixed)
}
